plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

def MAIN = "main"
def DEV = "dev"
def PROD = "prod"

def URL_PROD = uri("https://maven.pkg.github.com/etendosoftware/com.etendoerp.etendobackup")
def URL_DEV  = "https://repo.futit.cloud/repository/static-public-snapshots"
final String PLUGIN_VERSION = "1.1.0"

// Default mode
def mode = DEV
def pluginVersion = PLUGIN_VERSION

def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

if (gitBranch() == MAIN) {
    mode = PROD
}
if (mode == DEV) {
    pluginVersion += "-SNAPSHOT"
}

def urlToPublish = (mode == PROD) ? URL_PROD : URL_DEV

group 'com.etendoerp'
version(pluginVersion)

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            credentials {
                if (mode == PROD) {
                    username "$githubUser"
                    password "$githubToken"
                } else {
                    username "$nexusUser"
                    password "$nexusPassword"
                }
            }
            url(urlToPublish)
        }
    }
}

dependencies {
    implementation gradleApi()
    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4+'
    implementation 'com.sun.mail:javax.mail:1.6.0'
}

gradlePlugin {
    plugins {
        etendoBackupPlugin {
            id = "com.etendoerp.etendobackup"
            implementationClass = "com.etendoerp.EtendoBackupPlugin"
        }
    }
}

/**
 * Gradle task to upgrade the version of a plugin.
 * Supports incrementing major, minor, or patch versions.
 * Usage: ./gradlew upgradePluginVersion -Ptype=[major|minor|patch]
 */
task upgradePluginVersion() {
    final String MAJOR = 'major'
    final String MINOR = 'minor'
    final String PATCH = 'patch'
    doLast {
        def typeVersion
        if (project.hasProperty('type')){
            typeVersion = project.getProperty('type')
        } else {
            throw new GradleException("The parameter -Ptype is required")
        }

        if (typeVersion != MAJOR && typeVersion != MINOR && typeVersion != PATCH) {
            throw new GradleException("The parameter value '${typeVersion}' is not valid, the options should be 'major', 'minor' or 'patch'")
        }

        def version = PLUGIN_VERSION.trim().split("\\.")
        if (version.length != 3) {
            throw new GradleException("The version must be in the format major.minor.patch")
        }

        if (typeVersion == MAJOR) {
            version[0]++
            version[1] = 0
            version[2] = 0
        } else if (typeVersion == MINOR) {
            version[1]++
            version[2] = 0
        } else if (typeVersion == PATCH) {
            version[2]++
        }
        def nextVersion = "${version[0]}.${version[1]}.${version[2]}"

        //Rewrite build.gradle file with new value
        def buildGradleContent = file('build.gradle').text
        buildGradleContent = buildGradleContent.replaceAll(/PLUGIN_VERSION = \"${PLUGIN_VERSION}\"/, "PLUGIN_VERSION = \"${nextVersion}\"")
        file('build.gradle').write(buildGradleContent)
    }
}
